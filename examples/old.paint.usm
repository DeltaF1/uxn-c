( sprite )

:dev/r fff8 ( std read port )
:dev/w fff9 ( std write port )

&Point2d { x 2 y 2 }

;mouse Point2d
;state 1 ;color 1 ;brush 2

|0100 @RESET 

	#05 =dev/r ( set dev/read mouse )
	#02 =dev/w ( set dev/write to sprite ) 

	#05 =color ( select a default color )
	,brush_large =brush ( select a default brush )

	,draw-interface JSR 

BRK

|c000 @FRAME
	
	( clear last cursor )
	#10 ,clear_icn ~mouse.x ~mouse.y ,draw-sprite JSR
	( record mouse values )
	#00 IOR2 =mouse.x #02 IOR2 =mouse.y
	#04 IOR #11 ADD =state
	~state ,cursor_icn ~mouse.x ~mouse.y ,draw-sprite JSR

	( check click )
	#04 IOR #00 EQU ,end-touch ROT JMP? POP2

		( check interface )
		#00 IOR2 #0008 DIV2 #0001 NEQ2 ,end-interface ROT JMP? POP2

			@option0
			#02 IOR2 #0008 DIV2 #0004 NEQ2 ,option1 ROT JMP? POP2
				,brush_large =brush BRK
			@option1
			#02 IOR2 #0008 DIV2 #0005 NEQ2 ,option2 ROT JMP? POP2
				,brush_small =brush BRK
			@option2
			#02 IOR2 #0008 DIV2 #0006 NEQ2 ,option3 ROT JMP? POP2
				,brush_tiny =brush BRK
			@option3
			#02 IOR2 #0008 DIV2 #000b NEQ2 ,option4 ROT JMP? POP2
				#05 =color BRK
			@option4
			#02 IOR2 #0008 DIV2 #000c NEQ2 ,option5 ROT JMP? POP2
				#0a =color BRK
			@option5
			#02 IOR2 #0008 DIV2 #000d NEQ2 ,option6 ROT JMP? POP2
				#0f =color BRK
			@option6

			BRK
		@end-interface

		~color ~brush ~mouse.x #0004 SUB2 ~mouse.y #0004 SUB2 ,draw-sprite JSR
	@end-touch

BRK

@draw-interface
	#05 ,brush_large #0008 #0020 ,draw-sprite JSR
	#05 ,brush_small #0008 #0028 ,draw-sprite JSR
	#05 ,brush_tiny  #0008 #0030 ,draw-sprite JSR
	#05 ,brush_large #0008 #0058 ,draw-sprite JSR
	#0a ,brush_large #0008 #0060 ,draw-sprite JSR
	#0f ,brush_large #0008 #0068 ,draw-sprite JSR
	RTS

@draw-sprite
	IOW2 ( y byte )
	IOW2 ( x byte )
	IOW2 ( sprite address )
	IOW ( layer-color )
	RTS

|0200 @SPRITESHEET

@cursor_icn  [ 80c0 e0f0 f8e0 1000 ]
@clear_icn   [ 0000 0000 0000 0000 ]
@brush_large [ 387c fefe fe7c 3800 ]
@brush_small [ 0038 7c7c 7c38 0000 ]
@brush_tiny  [ 0000 1038 1000 0000 ]

BRK

|d000 @ERROR BRK 

|FFF0 .RESET .FRAME .ERROR
|FFF8 [ f1c3 f12e f12a ] ( palette )
