( drum rack 

	TODO
		- Change pitch of sample
		- Draw pad pitch on pad
		- When selecting a pad, should highlight note in octave
)


%RTN { JMP2r }
%8+ { #0008 ADD2 }
%8/ { #0008 DIV2 }
%2/ { #0002 DIV2 }
%2* { #0002 MUL2 }
%8* { #0008 MUL2 }
%++ { #0001 ADD2 }
%TOS { #00 SWP }
%MOD { DUP2 DIV MUL SUB }
%LTS2  { #8000 ADD2 SWP2 #8000 ADD2 GTH2 }
%GTS2  { #8000 ADD2 SWP2 #8000 ADD2 LTH2 }

%PAD-WIDTH { #0030 }
%PAD-HEIGHT { #0020 }
%STATE-LENGTH { #04 }

( devices )

|00 @System     [ &vector $2 &pad      $6 &r      $2 &g     $2 &b      $2 ]
|10 @Console    [ &pad    $8 &char     $1 &byte   $1 &short $2 &string $2 ]
|20 @Screen     [ &vector $2 &width    $2 &height $2 &pad   $2 &x      $2 &y     $2 &addr   $2 &color $1 ]
|30 @Audio0     [ &vector $2 &position $2 &output $1 &pad   $3 &adsr   $2 &length $2 &addr  $2 &volume $1 &pitch $1 ]
|40 @Audio1     [ &vector $2 &position $2 &output $1 &pad   $3 &adsr   $2 &length $2 &addr  $2 &volume $1 &pitch $1 ]
|50 @Audio2     [ &vector $2 &position $2 &output $1 &pad   $3 &adsr   $2 &length $2 &addr  $2 &volume $1 &pitch $1 ]
|60 @Audio3     [ &vector $2 &position $2 &output $1 &pad   $3 &adsr   $2 &length $2 &addr  $2 &volume $1 &pitch $1 ]
|70 @Midi       [ &vector $2 &channel  $1 &note   $1 &velocity $1 ]
|80 @Controller [ &vector $2 &button   $1 &key    $1 ]
|90 @Mouse      [ &vector $2 &x        $2 &y      $2 &state $1 &chord $1 ]
|a0 @File       [ &vector $2 &success  $2 &offset $2 &pad   $2 &name  $2 &length $2 &load $2 &save $2 ]
|b0 @DateTime   [ &year   $2 &month    $1 &day    $1 &hour $1 &minute $1 &second $1 &dotw $1 &doty $2 &isdst $1 ]

( variables )

|0000

@piano [ &last $1 &octave $1 ]

@last-note $1
@last $1
@color $1
@center      [ &x $2 &y $2 ]
@pointer     [ &x $2 &y $2 ]
@knob        [ &x $2 &y $2 &value $1 ]
@rect        [ &x1 $2 &y1 $2 &x2 $2 &y2 $2 ]
@frame       [ &x1 $2 &y1 $2 &x2 $2 &y2 $2 ]
@label       [ &x $2 &y $2 &color $1 &addr $2 ]
@pos         [ &x $2 &y $2 ] 
@pad-view     [ &x1 $2 &y1 $2 &x2 $2 &y2 $2 ]
@mix-view     [ &x1 $2 &y1 $2 &x2 $2 &y2 $2 ]
@oct-view     [ &x1 $2 &y1 $2 &x2 $2 &y2 $2 ]
@wav-view     [ &x1 $2 &y1 $2 &x2 $2 &y2 $2 ]

@state [ ( addr length )
	$4 $4 $4 $4
	$4 $4 $4 $4
	$4 $4 $4 $4
	$4 $4 $4 $4
]

@modes [ $1 $1 $1 $1 ] ( single/repeat )

( program )

|0100 ( -> )
	
	( theme ) 
	#0aff .System/r DEO2 
	#0a2f .System/g DEO2 
	#0a3f .System/b DEO2

	( vectors )
	;on-control .Controller/vector DEO2
	;on-mouse .Mouse/vector DEO2
	;on-frame .Screen/vector DEO2
	;on-midi .Midi/vector DEO2

	( store location/length )
	#dd .Audio0/volume DEO #023c .Audio0/adsr DEO2
	#ce .Audio1/volume DEO #1334 .Audio1/adsr DEO2
	#ec .Audio2/volume DEO #0414 .Audio2/adsr DEO2
	#ee .Audio3/volume DEO #022c .Audio3/adsr DEO2

	#0400 .Audio0/length DEO2
	#0800 .Audio1/length DEO2
	#0800 .Audio2/length DEO2
	#1000 .Audio3/length DEO2

	#01 .modes POK
	#03 .piano/octave POK

	;load-samples JSR2

	( find center )
	.Screen/width DEI2 2/ .center/x POK2
	.Screen/height DEI2 2/ .center/y POK2

	( place frame )
	.center/x PEK2 PAD-WIDTH #0003 MUL2 SUB2 #0010 SUB2 .frame/x1 POK2
	.center/y PEK2 #0050 SUB2 .frame/y1 POK2
	.center/x PEK2 PAD-WIDTH #0003 MUL2 ADD2 #0010 ADD2 .frame/x2 POK2
	.frame/y1 PEK2 PAD-HEIGHT #0004 MUL2 ADD2 #0028 ADD2 .frame/y2 POK2

	( place pad-view )
	.frame/x1 PEK2 .pad-view/x1 POK2
	.frame/y1 PEK2 .pad-view/y1 POK2
	.pad-view/x1 PEK2 #0004 PAD-WIDTH MUL2 ADD2 .pad-view/x2 POK2
	.pad-view/y1 PEK2 #0004 PAD-HEIGHT MUL2 ADD2 .pad-view/y2 POK2

	( place mix-view )
	.pad-view/x2 PEK2 #0006 ADD2 .mix-view/x1 POK2
	.pad-view/y1 PEK2 .mix-view/y1 POK2
	.mix-view/x1 PEK2 #007a ADD2 .mix-view/x2 POK2
	.pad-view/y2 PEK2 .mix-view/y2 POK2

	( place oct-view )
	.frame/x1 PEK2 .oct-view/x1 POK2
	.mix-view/y2 PEK2 #0004 ADD2 .oct-view/y1 POK2
	.oct-view/x1 PEK2 #0050 ADD2 .oct-view/x2 POK2
	.oct-view/y1 PEK2 #0018 ADD2 .oct-view/y2 POK2

	( place wav-view )
	.oct-view/x2 PEK2 .wav-view/x1 POK2
	.mix-view/y2 PEK2 #0004 ADD2 .wav-view/y1 POK2
	.frame/x2 PEK2 .wav-view/x2 POK2
	.wav-view/y1 PEK2 #0020 ADD2 .wav-view/y2 POK2

	;draw-pads JSR2
	;draw-mixers JSR2
	;draw-octave JSR2

	.mix-view/x1 PEK2 #0004 SUB2 
	.mix-view/y1 PEK2 #0000 SUB2 
	.mix-view/x2 PEK2 #0002 SUB2
	.mix-view/y2 PEK2 #0002 SUB2
	#01
	;line-rect JSR2

	.wav-view/x1 PEK2 #0000 SUB2 
	.wav-view/y1 PEK2 #0002 SUB2 
	.wav-view/x2 PEK2 #0002 SUB2
	.wav-view/y2 PEK2 #0002 ADD2
	#01
	;line-rect JSR2

BRK

@on-frame ( -> )
	
	( update VU monitors )
	#00 #04
	&loop
		OVR STH
		( x ) .mix-view/x1 PEK2 #0074 ADD2
		( y ) .mix-view/y1 PEK2 PAD-HEIGHT DUPr STHr TOS MUL2 ADD2 #0002 ADD2
		( output ) .Audio0/output STHr #10 MUL ADD DEI 
		;draw-monitor JSR2
		( incr ) SWP #01 ADD SWP
		DUP2 LTH ,&loop JNZ
	POP2

BRK

@on-mouse ( -> )

	;draw-cursor JSR2 

	.Mouse/state DEI #00 NEQ ,&on-touch JNZ BRK &on-touch

	.Mouse/x DEI2 DUP2 .pad-view/x1 PEK2 GTH2 ROT ROT .pad-view/x2 PEK2 #0001 ADD2 LTH2 #0101 EQU2
	.Mouse/y DEI2 DUP2 .pad-view/y1 PEK2 GTH2 ROT ROT .pad-view/y2 PEK2 LTH2 #0101 EQU2
	#0101 EQU2 ;on-touch-pad JNZ2

	.Mouse/x DEI2 DUP2 .mix-view/x1 PEK2 GTH2 ROT ROT .mix-view/x2 PEK2 #0001 ADD2 LTH2 #0101 EQU2
	.Mouse/y DEI2 DUP2 .mix-view/y1 PEK2 GTH2 ROT ROT .mix-view/y2 PEK2 LTH2 #0101 EQU2
	#0101 EQU2 ;on-touch-mix JNZ2

	.Mouse/x DEI2 DUP2 .oct-view/x1 PEK2 GTH2 ROT ROT .oct-view/x2 PEK2 #0001 ADD2 LTH2 #0101 EQU2
	.Mouse/y DEI2 DUP2 .oct-view/y1 PEK2 GTH2 ROT ROT .oct-view/y2 PEK2 LTH2 #0101 EQU2
	#0101 EQU2 ;on-touch-oct JNZ2

BRK

@on-midi ( -> )
	
	.Midi/note DEI #00 NEQ ,&note-on JNZ BRK &note-on

	.Midi/channel DEI #90 NEQ ,&no-drum JNZ 
		.Midi/note DEI #10 MOD ;play-pad JSR2 )
		BRK
	&no-drum

	;pad-addr #0008 ADD2 LDA2 .Audio0/addr DEO2
	#0008 .Audio0/length DEO2
	.Midi/note DEI .Audio0/pitch DEO

BRK

@on-control ( -> )

	.Controller/key DEI #00 NEQ ,&key-down JNZ BRK &key-down

	.Controller/key DEI 
	DUP #31 NEQ ,&no-0 JNZ #00 ;play-pad JSR2 &no-0
	DUP #32 NEQ ,&no-1 JNZ #01 ;play-pad JSR2 &no-1
	DUP #33 NEQ ,&no-2 JNZ #02 ;play-pad JSR2 &no-2
	DUP #34 NEQ ,&no-3 JNZ #03 ;play-pad JSR2 &no-3
	DUP #71 NEQ ,&no-4 JNZ #04 ;play-pad JSR2 &no-4
	DUP #77 NEQ ,&no-5 JNZ #05 ;play-pad JSR2 &no-5
	DUP #65 NEQ ,&no-6 JNZ #06 ;play-pad JSR2 &no-6
	DUP #72 NEQ ,&no-7 JNZ #07 ;play-pad JSR2 &no-7
	DUP #61 NEQ ,&no-8 JNZ #08 ;play-pad JSR2 &no-8
	DUP #73 NEQ ,&no-9 JNZ #09 ;play-pad JSR2 &no-9
	DUP #64 NEQ ,&no-a JNZ #0a ;play-pad JSR2 &no-a
	DUP #66 NEQ ,&no-b JNZ #0b ;play-pad JSR2 &no-b
	DUP #7a NEQ ,&no-c JNZ #0c ;play-pad JSR2 &no-c
	DUP #78 NEQ ,&no-d JNZ #0d ;play-pad JSR2 &no-d
	DUP #63 NEQ ,&no-e JNZ #0e ;play-pad JSR2 &no-e
	DUP #76 NEQ ,&no-f JNZ #0f ;play-pad JSR2 &no-f
	POP

BRK

@on-touch-pad ( -> )
	
	( x ) .Mouse/x DEI2 .pad-view/x1 PEK2 SUB2 PAD-WIDTH DIV2 SWP POP
	( y ) .Mouse/y DEI2 .pad-view/y1 PEK2 SUB2 PAD-HEIGHT DIV2 SWP POP #04 MUL ADD
	
	DUP SWP ;play-pad JSR2

	;draw-waveform JSR2

	( release ) #00 .Mouse/state DEO

BRK

@on-touch-mix ( -> )

	( channel ) .Mouse/y DEI2 .mix-view/y1 PEK2 SUB2 PAD-HEIGHT DIV2 SWP POP STH

	( knob ) .Mouse/x DEI2 .mix-view/x1 PEK2 SUB2 #0010 DIV2 SWP POP

	DUP #00 NEQ ,&no-a JNZ
		.Audio0/adsr [ DUPr STHr #10 MUL ADD ] DEI
		#10 .Mouse/state DEI #10 EQU #e0 MUL ADD ADD
		.Audio0/adsr [ DUPr STHr #10 MUL ADD ] DEO &no-a
	DUP #01 NEQ ,&no-d JNZ
		.Audio0/adsr [ DUPr STHr #10 MUL ADD ] DEI
		DUP #f0 AND STH #01 .Mouse/state DEI #10 EQU #0e MUL ADD ADD #0f AND STHr ADD
		.Audio0/adsr [ DUPr STHr #10 MUL ADD ] DEO &no-d
	DUP #02 NEQ ,&no-s JNZ
		.Audio0/adsr [ DUPr STHr #10 MUL ADD ] #01 ADD DEI
		#10 .Mouse/state DEI #10 EQU #e0 MUL ADD ADD
		.Audio0/adsr [ DUPr STHr #10 MUL ADD ] #01 ADD DEO &no-s
	DUP #03 NEQ ,&no-r JNZ
		.Audio0/adsr [ DUPr STHr #10 MUL ADD ] #01 ADD DEI
		DUP #f0 AND STH #01 .Mouse/state DEI #10 EQU #0e MUL ADD ADD #0f AND STHr ADD
		.Audio0/adsr [ DUPr STHr #10 MUL ADD ] #01 ADD DEO &no-r

	DUP #04 NEQ ,&no-repeat JNZ
		.modes [ DUPr STHr ADD ] PEK 
		#00 EQU
		.modes [ DUPr STHr ADD ] POK &no-repeat

	DUP #05 NEQ ,&no-left JNZ
		.Audio0/volume [ DUPr STHr #10 MUL ADD ] DEI 
		#10 .Mouse/state DEI #10 EQU #e0 MUL ADD ADD
		.Audio0/volume [ DUPr STHr #10 MUL ADD ] DEO &no-left
	DUP #06 NEQ ,&no-right JNZ
		.Audio0/volume [ DUPr STHr #10 MUL ADD ] DEI
		DUP #f0 AND STH #01 .Mouse/state DEI #10 EQU #0e MUL ADD ADD #0f AND STHr ADD
		.Audio0/volume [ DUPr STHr #10 MUL ADD ] DEO &no-right
	POP

	( release ) #00 .Mouse/state DEO

	DUPr STHr ;draw-mixer JSR2

	POPr

BRK

@on-touch-oct ( -> )
	
	.Mouse/x DEI2 .oct-view/x1 PEK2 SUB2 8/ SWP POP #08 NEQ ,&no-mod JNZ
		.Mouse/y DEI2 .oct-view/y1 PEK2 SUB2 8/ SWP POP 
		DUP #00 NEQ ,&no-incr JNZ
			.piano/octave PEK #01 ADD .piano/octave POK &no-incr
		DUP #02 NEQ ,&no-decr JNZ
			.piano/octave PEK #01 SUB .piano/octave POK &no-decr
		POP
		( release ) #00 .Mouse/state DEO
		;draw-octave JSR2
		BRK
	&no-mod

	.Mouse/x DEI2 .oct-view/x1 PEK2 SUB2 8/ SWP POP #06 GTH ,&no-key JNZ
		.Mouse/x DEI2 .oct-view/x1 PEK2 SUB2 8/ 

		( set pitch of pad )
		DUP2 ;notes ADD2 LDA .last PEK SWP .piano/octave PEK #02 SUB #0c MUL ADD ;set-pitch JSR2

		( record last note )
		SWP POP .last-note POK 
		.last PEK ;play-pad JSR2

		( release ) #00 .Mouse/state DEO
		;draw-octave JSR2
	&no-key

BRK

@set-pitch ( pad pitch -- )
	
	SWP TOS ;pad-pitch ADD2 STA

RTN

@play ( pitch -- )
	
	DUP .piano/octave PEK .modes PEK MUL ADD .Audio0/pitch DEO

RTN

@load-samples ( -- )

	#00 #10
	&loop
		( name ) OVR TOS #0019 MUL2 ;pad-path ADD2 .File/name DEO2
		( length ) OVR #04 DIV #10 MUL .Audio0/length ADD DEI2 .File/length DEO2
		( load ) OVR TOS #0002 MUL2 ;pad-addr ADD2 LDA2 .File/load DEO2
		( incr ) SWP #01 ADD SWP
		DUP2 LTH ,&loop JNZ
	POP2
	
RTN

@play-pad ( note pad -- )
	
	( unselect last )
	.last PEK #01 ;draw-pad JSR2
	DUP .last POK 
	( highlight )
	DUP #02 ;draw-pad JSR2
	( addr )
	DUP TOS #0002 MUL2 ;pad-addr ADD2 LDA2 STH2
	DUP #04 DIV #10 MUL .Audio0/addr ADD STH2r ROT DEO2
	( pitch )
	DUP TOS ;pad-pitch ADD2 LDA STH 
	DUP #04 DIV .modes ADD PEK #00 EQU #80 MUL STH ADDr
	DUP #04 DIV #10 MUL .Audio0/pitch ADD STHr SWP DEO
	#04 DIV ;draw-mixer JSR2

RTN

@draw-waveform ( pad -- )
	
	#02 MUL #00 SWP ;pad-addr ADD2 LDA2 STH2

	.wav-view/x1 PEK2 #0002 ADD2
	.wav-view/y1 PEK2
	.wav-view/x2 PEK2 #0002 SUB2
	.wav-view/y2 PEK2
	#00 ;fill-rect JSR2

	.wav-view/x1 PEK2 #0002 ADD2 .Screen/x DEO2

	( waveform )
	#00 #e8
	&loop
		( dotted line )
		OVR #01 AND ,&no-dot JNZ 
			.wav-view/y1 PEK2 #0010 ADD2 .Screen/y DEO2
			#01 .Screen/color DEO
		&no-dot
		OVR TOS #0010 MUL2 [ DUP2r STH2r ] ADD2 LDA 
		#02 DIV #40 ADD DUP #07 SFT #80 MUL SUB 
		TOS #0004 DIV2 .wav-view/y1 PEK2 ADD2 .Screen/y DEO2
		.Screen/x DEI2 ++ .Screen/x DEO2
		( draw ) #02 .Screen/color DEO
		( incr ) SWP #01 ADD SWP
		DUP2 LTH ,&loop JNZ
	POP2

	POP2r

RTN

@draw-mixer ( mixer -- )

	DUP STH #00 SWP PAD-HEIGHT MUL2 .mix-view/y1 PEK2 ADD2 #0003 ADD2

	( adsr )
	DUP2 .mix-view/x1 PEK2 SWP2
		.Audio0/adsr [ DUPr STHr #10 MUL ADD ] DEI #04 SFT
		;draw-knob JSR2
	DUP2 .mix-view/x1 PEK2 #0010 ADD2 SWP2
		.Audio0/adsr [ DUPr STHr #10 MUL ADD ] DEI #0f AND
		;draw-knob JSR2
	DUP2 .mix-view/x1 PEK2 #0020 ADD2 SWP2
		.Audio0/adsr [ DUPr STHr #10 MUL ADD ] #01 ADD DEI #04 SFT
		;draw-knob JSR2
	DUP2 .mix-view/x1 PEK2 #0030 ADD2 SWP2
		.Audio0/adsr [ DUPr STHr #10 MUL ADD ] #01 ADD DEI #0f AND
		;draw-knob JSR2
	( once/repeat )
	DUP2 .mix-view/x1 PEK2 #0040 ADD2 SWP2
		.modes [ DUPr STHr ADD ] PEK ;draw-switch JSR2
	( volume )
	DUP2 .mix-view/x1 PEK2 #0050 ADD2 SWP2
		.Audio0/volume [ DUPr STHr #10 MUL ADD ] DEI #04 SFT
		;draw-knob JSR2
	DUP2 .mix-view/x1 PEK2 #0060 ADD2 SWP2
		.Audio0/volume [ DUPr STHr #10 MUL ADD ] DEI #0f AND
		;draw-knob JSR2
	POP2
	POPr

RTN

@draw-knob ( x* y* value -- )

	( load ) .knob/value POK .knob/y POK2 .knob/x POK2

	.knob/x PEK2 .Screen/x DEO2
	.knob/y PEK2 .Screen/y DEO2 ;knob-icns .Screen/addr DEO2 #21 .Screen/color DEO
	.knob/x PEK2 8+ .Screen/x DEO2 ;knob-icns 8+ .Screen/addr DEO2 #21 .Screen/color DEO
	.knob/y PEK2 8+ .Screen/y DEO2 ;knob-icns #0018 ADD2 .Screen/addr DEO2 #21 .Screen/color DEO
	.knob/x PEK2 .Screen/x DEO2 ;knob-icns #0010 ADD2 .Screen/addr DEO2 #21 .Screen/color DEO
	.knob/x PEK2 #00 #00 .knob/value PEK ;knob-offsetx ADD2 LDA ADD2 .Screen/x DEO2
	.knob/y PEK2 #00 #00 .knob/value PEK ;knob-offsety ADD2 LDA ADD2 .Screen/y DEO2
	;knob-icns #0020 ADD2 .Screen/addr DEO2
	#25 .Screen/color DEO
	.knob/x PEK2 #0004 ADD2 .Screen/x DEO2
	.knob/y PEK2 #0010 ADD2 .Screen/y DEO2
	;font-hex #00 .knob/value PEK #08 MUL ADD2 .Screen/addr DEO2
	#21 .Screen/color DEO

RTN

@draw-switch ( x* y* value -- )
	
	STH 

	.Screen/y DEO2
	.Screen/x DEO2

	DUPr STHr #20 MUL #00 SWP ;switch-icns ADD2 ( keep in stack )

	DUP2 .Screen/addr DEO2
	#21 DUPr STHr ADD .Screen/color DEO

	.Screen/x DEI2 #0008 ADD2 .Screen/x DEO2
	DUP2 #0008 ADD2 .Screen/addr DEO2
	#21 DUPr STHr ADD .Screen/color DEO

	.Screen/y DEI2 #0008 ADD2 .Screen/y DEO2
	.Screen/x DEI2 #0008 SUB2 .Screen/x DEO2
	DUP2 #0010 ADD2 .Screen/addr DEO2
	#21 DUPr STHr ADD .Screen/color DEO

	.Screen/x DEI2 #0008 ADD2 .Screen/x DEO2
	#0018 ADD2 .Screen/addr DEO2
	#21 STHr ADD .Screen/color DEO

RTN

@draw-pads ( -- )
	
	#00 #10
	&loop
		OVR #01 ;draw-pad JSR2
		SWP #01 ADD SWP
		DUP2 LTH ,&loop JNZ
	POP2

RTN

@draw-mixers ( -- )
	
	#00 #04
	&loop
		OVR ;draw-mixer JSR2
		SWP #01 ADD SWP
		DUP2 LTH ,&loop JNZ
	POP2

RTN

@draw-pad ( pad color -- )
	
	STH STH

	DUPr STHr 

	DUP #04 DIV SWP #04 MOD TOS PAD-WIDTH MUL2 ( center ) .pad-view/x1 PEK2 ADD2 
	ROT TOS PAD-HEIGHT MUL2 ( center ) .pad-view/y1 PEK2 ADD2 

	( draw outline )
	OVR2 OVR2 
	OVR2 PAD-WIDTH #0002 SUB2 ADD2 
	OVR2 PAD-HEIGHT #0002 SUB2 ADD2 
	OVRr STHr ;line-rect JSR2

	( draw name )
	OVR2 #0002 ADD2 
	OVR2 #0002 ADD2 
	#20 OVRr STHr ADD 
	;pad-name DUPr STHr TOS #0005 MUL2 ADD2 
	;draw-label-left JSR2

	( draw note )
	OVR2 #0003 ADD2 .Screen/x DEO2
	DUP2 #0014 ADD2 .Screen/y DEO2
	( get pitch ) DUPr STHr TOS ;pad-pitch ADD2 LDA
	#0c DIV TOS #0008 MUL2 ;font-hex ADD2 .Screen/addr DEO2
	#21 .Screen/color DEO

	( draw octave )
	OVR2 #000b ADD2 .Screen/x DEO2
	DUP2 #0014 ADD2 .Screen/y DEO2
	( get pitch ) DUPr STHr TOS ;pad-pitch ADD2 LDA
	#0c MOD TOS #0008 MUL2 ;font-notes ADD2 .Screen/addr DEO2
	#21 .Screen/color DEO

	POP2 POP2

	POPr POPr

RTN

@draw-octave ( -- )
	
	.oct-view/x1 PEK2 .oct-view/y1 PEK2

	OVR2 OVR2 ;keys-left-icns #21 .last-note PEK #00 EQU ADD ;draw-key JSR2
	OVR2 8+ OVR2 ;keys-middle-icns #21 .last-note PEK #01 EQU ADD ;draw-key JSR2
	OVR2 #0010 ADD2 OVR2 ;keys-right-icns #21 .last-note PEK #02 EQU ADD ;draw-key JSR2
	OVR2 #0018 ADD2 OVR2 ;keys-left-icns #21 .last-note PEK #03 EQU ADD ;draw-key JSR2
	OVR2 #0020 ADD2 OVR2 ;keys-middle-icns #21 .last-note PEK #04 EQU ADD ;draw-key JSR2
	OVR2 #0028 ADD2 OVR2 ;keys-middle-icns #21 .last-note PEK #05 EQU ADD ;draw-key JSR2
	SWP2 #0030 ADD2 SWP2 ;keys-right-icns #21 .last-note PEK #06 EQU ADD ;draw-key JSR2

	.oct-view/x1 PEK2 #0040 ADD2 .Screen/x DEO2

	;arrow-icns .Screen/addr DEO2
	.oct-view/y1 PEK2 .Screen/y DEO2
	#21 .Screen/color DEO

	;arrow-icns 8+ .Screen/addr DEO2
	.oct-view/y1 PEK2 #0010 ADD2 .Screen/y DEO2
	#21 .Screen/color DEO

	;font-hex .piano/octave PEK #03 ADD #00 SWP #0008 MUL2 ADD2 .Screen/addr DEO2
	.oct-view/y1 PEK2 8+ .Screen/y DEO2
	#23 .Screen/color DEO

RTN

@draw-monitor ( x* y* stereo -- )

	STH 

	.Screen/y DEO2
	.Screen/x DEO2

	#00 #0f
	&loop
		OVR #10 SWP SUB DUPr STHr #0f AND LTH #01 ADD .Screen/color DEO
		.Screen/x DEI2 #0002 ADD2 .Screen/x DEO2
		OVR #10 SWP SUB DUPr STHr #04 SFT LTH #01 ADD .Screen/color DEO
		.Screen/x DEI2 #0002 SUB2 .Screen/x DEO2
		.Screen/y DEI2 #0002 ADD2 .Screen/y DEO2
		( incr ) SWP #01 ADD SWP
		DUP2 LTH ,&loop JNZ
	POP2

	POPr

RTN

@draw-key ( x* y* addr* color -- )
		
	STH
	.Screen/addr DEO2 
	SWP2 .Screen/x DEO2
	DUP2 #0018 ADD2
	&loop
		( move ) OVR2 .Screen/y DEO2
		( draw ) DUPr STHr .Screen/color DEO
		( incr ) .Screen/addr DEI2 8+ .Screen/addr DEO2  
		( incr ) SWP2 8+ SWP2
		OVR2 OVR2 LTH2 ,&loop JNZ
	POP2 POP2 
	POPr

RTN

@draw-cursor ( -- )
	
	( clear last cursor )
	#fff8 .Screen/addr DEO2 
	.pointer/x PEK2 .Screen/x DEO2 
	.pointer/y PEK2 .Screen/y DEO2 
	#30 .Screen/color DEO
	( record pointer positions )
	.Mouse/x DEI2 .pointer/x POK2 
	.Mouse/y DEI2 .pointer/y POK2
	( draw new cursor )
	;cursor-icn .Screen/addr DEO2 
	.pointer/x PEK2 .Screen/x DEO2 
	.pointer/y PEK2 .Screen/y DEO2 
	( colorize on state )
	#33 [ .Mouse/state DEI #00 NEQ ] SUB .Screen/color DEO

RTN

@draw-short ( addr color -- )

	STH SWP 
	DUP #04 SFT TOS 8* ;font-hex ADD2 .Screen/addr DEO2
	( draw ) DUPr STHr .Screen/color DEO
	#0f AND TOS 8* ;font-hex ADD2 .Screen/addr DEO2
	.Screen/x DEI2 8+ .Screen/x DEO2
	( draw ) DUPr STHr .Screen/color DEO
	DUP #04 SFT TOS 8* ;font-hex ADD2 .Screen/addr DEO2
	.Screen/x DEI2 8+ .Screen/x DEO2
	( draw ) DUPr STHr .Screen/color DEO
	#0f AND TOS 8* ;font-hex ADD2 .Screen/addr DEO2
	.Screen/x DEI2 8+ .Screen/x DEO2
	( draw ) STHr .Screen/color DEO

RTN

@line-rect ( x1 y1 x2 y2 color -- )

	( load ) .color POK DUP2 STH2 .rect/y2 POK2 .rect/x2 POK2 DUP2 STH2 .rect/y1 POK2 .rect/x1 POK2
	STH2r #0001 ADD2 STH2r
	&ver
		( save ) OVR2 .Screen/y DEO2
		( draw ) .rect/x1 PEK2 .Screen/x DEO2 .color PEK DUP .Screen/color DEO
		( draw ) .rect/x2 PEK2 .Screen/x DEO2 .Screen/color DEO
		( incr ) SWP2 ++ SWP2
		OVR2 OVR2 LTS2 ,&ver JNZ
	POP2 POP2
	.rect/x1 PEK2 #0001 ADD2 .rect/x2 PEK2 #0001 SUB2
	&hor
		( save ) OVR2 .Screen/x DEO2
		( draw ) .rect/y1 PEK2 .Screen/y DEO2 .color PEK DUP .Screen/color DEO
		( draw ) .rect/y2 PEK2 .Screen/y DEO2 .Screen/color DEO
		( incr ) SWP2 ++ SWP2
		OVR2 OVR2 ++ LTS2 ,&hor JNZ
	POP2 POP2

RTN

@draw-label-left ( x y color addr -- )
	
	( load ) .label/addr POK2 .label/color POK .Screen/y DEO2 .Screen/x DEO2
	.label/addr PEK2
	&loop
		( draw ) DUP2 LDA #00 SWP #0030 SUB2 #0008 MUL2 ;font-num-uc ADD2 .Screen/addr DEO2 .label/color PEK .Screen/color DEO
		( incr ) #0001 ADD2
		( incr ) .Screen/x DEI2 #0008 ADD2 .Screen/x DEO2
		DUP2 LDA #00 NEQ ,&loop JNZ
	POP2

RTN

@fill-rect ( x1* y1* x2* y2* color -- )
	
	.color POK
	( x1 x2 y1 y2 ) ROT2 SWP2
	&ver
		( save ) OVR2 .Screen/y DEO2
		STH2 STH2 OVR2 OVR2
		&hor
			( save ) OVR2 .Screen/x DEO2
			( draw ) .color PEK .Screen/color DEO
			( incr ) SWP2 ++ SWP2
			OVR2 OVR2 LTS2 ,&hor JNZ
		POP2 POP2 STH2r STH2r
		( incr ) SWP2 ++ SWP2
		OVR2 OVR2 LTS2 ,&ver JNZ
	POP2 POP2 POP2 POP2

RTN

@pad-name [
	"PAD1 $1 "SYN1 $1 "SYN2 $1 "CYM1 $1 ( short )
	"HHAT $1 "OHAT $1 "CHAT $1 "RIDE $1
	"SID1 $1 "SNR1 $1 "SNR2 $1 "SID2 $1 ( long )
	"BDR1 $1 "KCK1 $1 "KCK2 $1 "SUB1 $1 ]

@pad-addr [
	5000 5400 5800 5c00
	5000 5800 6000 6800
	7000 7800 8000 8800
	9000 b000 d000 f000 $2 ]

@pad-pitch [
	30 30 30 30
	30 30 30 30
	30 30 30 30
	30 30 30 30 ]

@pad-path [
	"projects/sounds/pad1.pcm $1 "projects/sounds/syn1.pcm $1
	"projects/sounds/syn2.pcm $1 "projects/sounds/cym1.pcm $1
	"projects/sounds/hhat.pcm $1 "projects/sounds/ohat.pcm $1
	"projects/sounds/chat.pcm $1 "projects/sounds/ride.pcm $1
	"projects/sounds/sid1.pcm $1 "projects/sounds/snr1.pcm $1
	"projects/sounds/snr2.pcm $1 "projects/sounds/sid2.pcm $1
	"projects/sounds/bdr1.pcm $1 "projects/sounds/kck1.pcm $1
	"projects/sounds/kck2.pcm $1 "projects/sounds/sub1.pcm $1 ]

@notes [
	3c 3e 40 41 43 45 47
	48 4a 4c 4d 4f 51 53 ]

@cursor-icn [ 
	80c0 e0f0 f8e0 1000 ]

@keys-left-icns [
	7c7c 7c7c 7c7c 7c7c
	7c7c 7c7c 7c7c 7e7f
	7f7f 7f7f 7f7f 3e00 ]

@keys-middle-icns [
	1c1c 1c1c 1c1c 1c1c
	1c1c 1c1c 1c1c 3e7f
	7f7f 7f7f 7f7f 3e00 ]

@keys-right-icns [
	1f1f 1f1f 1f1f 1f1f
	1f1f 1f1f 1f1f 3f7f
	7f7f 7f7f 7f7f 3e00 ]

@arrow-icns [
	0010 387c fe10 1000
	0010 1010 fe7c 3810 ]

@switch-icns [
	001f 2040 4040 4040
	00f8 0402 0202 0202
	404f 5f5f 4f20 1f00
	02f2 fafa f204 f800
	001f 204f 5f5f 4f40
	00f8 04f2 fafa f202
	4040 4040 4020 1f00
	0202 0202 0204 f800
]

@knob-icns [
	0003 0c10 2020 4040
	00c0 3008 0404 0202
	4040 2020 100c 0300
	0202 0404 0830 c000
	0000 183c 3c18 0000 ]

@knob-offsetx [
	01 00 00 00 00 01 02 03
	05 06 07 08 08 08 08 07 ]

@knob-offsety [
	07 06 05 03 02 01 00 00
	00 00 01 02 03 05 06 07 ]

@meter [
	0000 0000 0000 0000
	0000 0000 0000 0070 
	0000 0000 0000 007e
	0000 0000 0000 707e
	0000 0000 0070 7e7e
	0000 0000 007e 7e7e
	0000 0000 707e 7e7e
	0000 0000 7e7e 7e7e
	0000 0070 7e7e 7e7e
	0000 007e 7e7e 7e7e
	0000 707e 7e7e 7e7e
	0000 7e7e 7e7e 7e7e
	0070 7e7e 7e7e 7e7e
	007e 7e7e 7e7e 7e7e
	707e 7e7e 7e7e 7e7e
	7e7e 7e7e 7e7e 7e7e ]

@font-notes [
	003e 4140 4040 413e
	40ae 4100 4040 413e
	007e 4141 4141 417e
	40ae 4101 4141 417e
	003e 4140 7c40 413e
	003f 4040 7e40 4040
	40af 4000 7e40 4040
	003e 4140 5f41 413e
	40ae 4100 5f41 413e
	003e 4141 7f41 4141
	40ae 4101 7f41 4141
	007e 4141 7e41 417e
]

@font-hex ( 0-F )
[
	007c 8282 8282 827c 0030 1010 1010 1010
	007c 8202 7c80 80fe 007c 8202 1c02 827c
	000c 1424 4484 fe04 00fe 8080 7c02 827c
	007c 8280 fc82 827c 007c 8202 1e02 0202
	007c 8282 7c82 827c 007c 8282 7e02 827c
	007c 8202 7e82 827e 00fc 8282 fc82 82fc
	007c 8280 8080 827c 00fc 8282 8282 82fc
	007c 8280 f080 827c 007c 8280 f080 8080
]

@font-num-uc [
	003e 4141 4141 413e 0018 0808 0808 081c 
	003e 4101 3e40 407f 003e 4101 1f01 413e 
	0011 2141 7f01 0101 007f 4040 7e01 413e 
	003e 4140 7e41 413e 003e 4101 0102 0408 
	003e 4141 3e41 413e 003e 4141 3f01 0102 
	0000 0800 0000 0800 0000 0800 0000 0800 
	0000 0800 0000 0810 0000 0408 1008 0400 
	0000 001c 001c 0000 0000 1008 0408 1000 
	0000 0000 0000 0000 003e 4101 3f41 413f 
	007e 4141 7e41 417e 003e 4140 4040 413e 
	007e 4141 4141 417e 007f 4040 7e40 407f 
	007f 4040 7e40 4040 003e 4140 5e41 413e 
	0041 4141 7f41 4141 0008 0808 0808 0808 
	007f 0101 0101 413e 0041 4244 7844 4241 
	0040 4040 4040 403f 0076 4949 4949 4949 
	005e 6141 4141 4141 003e 4141 4141 413e 
	007e 4141 7e40 4040 003e 4141 4145 423d 
	007e 4141 7e41 4141 003e 4140 3e01 413e 
	007f 0808 0808 0808 0041 4141 4141 433d 
	0041 4141 4122 1408 0049 4949 4949 4976 
	0041 2214 0814 2241 0041 4141 3f01 413e 
	007f 0101 3e40 407f ]
