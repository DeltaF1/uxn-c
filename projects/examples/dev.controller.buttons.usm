( dev/controller/buttons )

%++ { #0001 ADD2 } 
%-- { #0001 SUB2 }
%2/ { #0002 DIV2 }

%%^! { .% DEI }  %%~! { .% DEO }
%%*! { .% DEI2 } %%=! { .% DEO2 }
%%^  { .% PEK }  %%~  { .% POK }
%%*  { .% PEK2 } %%=  { .% POK2 }

( devices )

|00 @System     [ &vector $2 &pad    $6 &r      $2 &g     $2 &b      $2 ]
|20 @Screen     [ &vector $2 &width  $2 &height $2 &pad   $2 &x      $2 &y $2 &addr $2 &color $1 ]
|40 @Controller [ &vector $2 &button $1 &key    $1 ] 

( variables )

|0000

@slime $1

( init )

|0100 ( -> )
	
	( theme ) 
	#0daf System/r=! 
	#02ff System/g=! 
	#035f System/b=!

	( vectors ) 
	;on-frame Screen/vector=!
	
	( set origin )
	Screen/width*! 2/ Screen/x=!
	Screen/height*! 2/ Screen/y=!

	;default_icn Screen/addr=!
	#31 Screen/color~!
	#2a slime~

BRK

@on-frame ( -> )

	#2a slime~
	;default_icn Screen/addr=!

	( hold ctrl key to change slime color )
	Controller/button^! #0f AND
		DUP #01 NEQ ,&no-ctrl JNZ #25 slime~ &no-ctrl
		DUP #02 NEQ ,&no-alt JNZ #2f slime~ &no-alt
	POP

	( clear ) #30 Screen/color~!

	( detect movement )
	Controller/button^! #f0 AND
		DUP #04 SFT #01 AND #01 NEQ ,&no-up JNZ 
			( move ) 
			Screen/y*! -- Screen/y=! 
			;up_icn Screen/addr=! &no-up
		DUP #05 SFT #01 AND #01 NEQ ,&no-down JNZ 
			( move ) 
			Screen/y*! ++ Screen/y=! 
			;down_icn Screen/addr=! &no-down
		DUP #06 SFT #01 AND #01 NEQ ,&no-left JNZ 
			( move ) 
			Screen/x*! -- Screen/x=! 
			;left_icn Screen/addr=! &no-left
		DUP #07 SFT #01 AND #01 NEQ ,&no-right JNZ 
			( move ) 
			Screen/x*! ++ Screen/x=! 
			;right_icn Screen/addr=! &no-right
	POP

	( draw face )
	#31 Screen/color~!

	( draw slime )
	;slime_icn Screen/addr=!
	slime^ Screen/color~!

BRK

@default_icn [ 3c7e ffdb ffe7 7e3c ]
@up_icn      [ 2466 e7db ffff 7e3c ]
@down_icn    [ 3c7e ffff dbe7 6624 ]
@left_icn    [ 3c7e ef1f 1fef 7e3c ]
@right_icn   [ 3c7e f7f8 f8f7 7e3c ]
@slime_icn   [ 0000 183c 3c18 0000 ]

