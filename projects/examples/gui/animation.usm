( GUI Animation )

%2// { #01 SFT2 }

( devices )

|00 @System     [ &vector $2 &pad    $6 &r      $2 &g     $2 &b      $2 ]
|20 @Screen     [ &vector $2 &width  $2 &height $2 &pad   $2 &x      $2 &y $2 &addr $2 &color $1 ]

|0000

@dvd [ &x $2 &y $2 &dx $1 &dy $1 ]

( program )

|0100 ( -> )
	
	( theme ) 
	#4cfd .System/r DEO2 
	#4cf3 .System/g DEO2 
	#dcf2 .System/b DEO2

	( vectors )
	;on-frame .Screen/vector DEO2

	( starting position )
	.Screen/width DEI2 2// .dvd/x STZ2
	.Screen/height DEI2 2// .dvd/y STZ2
	
	#21 ;draw-dvd JSR2
	
BRK

@on-frame ( -> )

	( clear ) #20 ;draw-dvd JSR2
	( case: hit-right ) .dvd/x LDZ2 .Screen/width DEI2 #0020 SUB2 EQU2
	( case: hit-left ) .dvd/x LDZ2 #0000 EQU2 
	#0000 EQU2 ,&no-flipx JCN 
		.dvd/dx LDZ #00 EQU .dvd/dx STZ &no-flipx
	( case: hit-bottom ) .dvd/y LDZ2 .Screen/height DEI2 #0010 SUB2 EQU2
	( case: hit-top ) .dvd/y LDZ2 #0000 EQU2 
	#0000 EQU2 ,&no-flipy JCN 
		.dvd/dy LDZ #00 EQU .dvd/dy STZ &no-flipy
	( incr ) .dvd/x LDZ2 #0001 #00 .dvd/dx LDZ #00 EQU #fffe MUL2 ADD2 ADD2 .dvd/x STZ2
	( incr ) .dvd/y LDZ2 #0001 #00 .dvd/dy LDZ #00 EQU #fffe MUL2 ADD2 ADD2 .dvd/y STZ2
	( draw ) #21 ;draw-dvd JSR2

BRK

@draw-dvd ( color -- )
	
	( stash ) STH 
	;dvd_icn .Screen/addr DEO2
	.dvd/y LDZ2 [ .dvd/y LDZ2 #0010 ADD2 ]
	&ver
		OVR2 .Screen/y DEO2
		.dvd/x LDZ2 [ .dvd/x LDZ2 #0020 ADD2 ]
		&hor
			OVR2 .Screen/x DEO2
			( draw ) STHkr .Screen/color DEO
			( next ) .Screen/addr DEI2 #0008 ADD2 .Screen/addr DEO2
			( incr ) SWP2 #0008 ADD2 SWP2
			LTH2k ,&hor JCN
		POP2 POP2
		( incr ) SWP2 #0008 ADD2 SWP2
		LTH2k ,&ver JCN
	POP2 POP2
	( destroy ) POPr

JMP2r

@dvd_icn [
	001f 3f38 3838 787f 00fe fe7e 7777 e3c3
	000f 1f3b 7b77 e7c7 00fc fe8f 8707 0efc
	7f00 000f ff7f 0700 0301 00ff f0f8 ff00
	8700 00ff 7f7f ff00 f000 00e0 fcfc 8000
]