( Controller )

%++ { #0001 ADD2 }
%-- { #0001 SUB2 }
%2/ { #0002 DIV2 }

( variables )

;slime { color 1 }

( devices )

|0100 ;Console { pad 8 char 1 byte 1 short 2 }
|0110 ;Screen { width 2 height 2 pad 4 y 2 x 2 color 1 }
|0120 ;Sprite { pad 8 x 2 y 2 addr 2 color 1 }
|0130 ;Controller { p1 1 }
|01F0 .RESET .FRAME .ERROR ( vectors )
|01F8 [ 0daf 02ff 035f ] ( palette )

|0200 @RESET 
	
	( set origin )
	~Screen.width 2/ =Sprite.x 
	~Screen.height 2/ =Sprite.y
	,default_icn =Sprite.addr
	#11 =Sprite.color
	#0a =slime

BRK

@FRAME 

	,on-ctrl-arrows ~Controller.p1 JNZ2

BRK

@on-ctrl-arrows

	#0a =slime
	,default_icn =Sprite.addr

	( hold ctrl key to change slime color )

	~Controller.p1 #0f AND
		DUP #01 NEQ ,$no-ctrl ROT JNZ2 #05 =slime $no-ctrl
		DUP #02 NEQ ,$no-alt ROT JNZ2 #0f =slime $no-alt
	POP

	( clear ) #10 =Sprite.color

	( detect movement )
	~Controller.p1 #f0 AND
		DUP #04 SFT #01 AND #01 NEQ ,$no-up ROT JNZ2 
			( move ) ~Sprite.y -- =Sprite.y ,up_icn =Sprite.addr $no-up
		DUP #05 SFT #01 AND #01 NEQ ,$no-down ROT JNZ2 
			( move ) ~Sprite.y ++ =Sprite.y ,down_icn =Sprite.addr $no-down
		DUP #06 SFT #01 AND #01 NEQ ,$no-left ROT JNZ2 
			( move ) ~Sprite.x -- =Sprite.x ,left_icn =Sprite.addr $no-left
		DUP #07 SFT #01 AND #01 NEQ ,$no-right ROT JNZ2 
			( move ) ~Sprite.x ++ =Sprite.x ,right_icn =Sprite.addr $no-right
	POP

	( draw face )
	#11 =Sprite.color

	( draw slime )
	,slime_icn =Sprite.addr
	~slime =Sprite.color

BRK

@default_icn [ 3c7e ffdb ffe7 7e3c ]
@up_icn      [ 2466 e7db ffff 7e3c ]
@down_icn    [ 3c7e ffff dbe7 6624 ]
@left_icn    [ 3c7e ef1f 1fef 7e3c ]
@right_icn   [ 3c7e f7f8 f8f7 7e3c ]
@slime_icn   [ 0000 183c 3c18 0000 ]

@ERROR BRK 
