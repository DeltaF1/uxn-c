( Controller )

%INCR2 { #0001 ADD2 }
%DECR2 { #0001 SUB2 }
%HALF2 { #0002 DIV2 }
%JMC2  { ROT JMP2? POP2 }

;slime { color 1 }

|0100 @RESET 
	
	( set origin )
	~Screen.width HALF2 =Sprite.x 
	~Screen.height HALF2 =Sprite.y
	,default_icn =Sprite.addr
	#11 =Sprite.color
	#0a =slime

BRK

@FRAME 
	
	~Controller.buttons #f0 AND #00 EQU BRK?

	#0a =slime

	( hold ctrl key to change slime color )

	~Controller.buttons #0f AND
		DUP #01 NEQ ,$no-ctrl JMC2 #05 =slime $no-ctrl
		DUP #02 NEQ ,$no-alt JMC2 #0f =slime $no-alt
	POP

	( clear ) #10 =Sprite.color

	( detect movement )
	~Controller.buttons #f0 AND
		DUP #04 SHR #01 AND #01 NEQ ,$no-up JMC2 
			( move ) ~Sprite.y DECR2 =Sprite.y ,up_icn =Sprite.addr $no-up
		DUP #05 SHR #01 AND #01 NEQ ,$no-down JMC2 
			( move ) ~Sprite.y INCR2 =Sprite.y ,down_icn =Sprite.addr $no-down
		DUP #06 SHR #01 AND #01 NEQ ,$no-left JMC2 
			( move ) ~Sprite.x DECR2 =Sprite.x ,left_icn =Sprite.addr $no-left
		DUP #07 SHR #01 AND #01 NEQ ,$no-right JMC2 
			( move ) ~Sprite.x INCR2 =Sprite.x ,right_icn =Sprite.addr $no-right
	POP

	( draw face )
	#11 =Sprite.color

	( draw slime )
	,slime_icn =Sprite.addr
	~slime =Sprite.color

BRK

@default_icn [ 3c7e ffdb ffe7 7e3c ]
@up_icn      [ 2466 e7db ffff 7e3c ]
@down_icn    [ 3c7e ffff dbe7 6624 ]
@left_icn    [ 3c7e ef1f 1fef 7e3c ]
@right_icn   [ 3c7e f7f8 f8f7 7e3c ]
@slime_icn   [ 0000 183c 3c18 0000 ]

|d000 @ERROR BRK 

|FF10 ;Screen { width 2 height 2 pad 4 y 2 x 2 color 1 }
|FF20 ;Sprite { pad 8 x 2 y 2 addr 2 color 1 }
|FF30 ;Controller { buttons 1 }

|FFF0 .RESET .FRAME .ERROR ( vectors )
|FFF8 [ 0daf 02ff 035f ] ( palette )
